// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {

    ext {
        kotlinVersion = '1.3.61'
    }
    repositories {
        google()
        jcenter()
        mavenCentral()
        mavenLocal()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
        classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:1.0.4"
        classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.15.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.25.0'
        classpath 'com.google.gms:google-services:4.3.3'
        classpath 'io.fabric.tools:gradle:1.31.2'
        classpath 'com.google.firebase:perf-plugin:1.3.1'
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "1.1.1"
}

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.1.1"
}

tasks.register('detektAll', io.gitlab.arturbosch.detekt.Detekt) {
    description = "Runs detekt analysis over whole code base without the starting overhead for each module."
    parallel = true
    config = files("$rootDir/default-detekt-config.yml")
    setSource(files(projectDir))
    include("**/*.kt")
    include("**/*.kts")
    exclude("**/resources/**")
    exclude("**/build/**")
    reports {
        xml.enabled = true
        html.enabled = false
        txt.enabled = false
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url "https://clojars.org/repo/" }
        maven { url "https://maven.fabric.io/public" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply from: 'buildsystem/dependencies.gradle'
apply from: 'buildsystem/bitrise.gradle'
apply from: 'buildsystem/secrets.gradle'
