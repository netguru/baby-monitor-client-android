apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.frogermcs.androiddevmetrics'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'org.sonarqube'
apply plugin: 'com.vanniktech.android.junit.jacoco'
apply from: '../buildsystem/sonarqube.gradle'

ext {

    secrets = [
            hockeyAppIdProduction: keyProperty("HOCKEY_APP_ID_PRODUCTION")
    ]
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    defaultConfig {
        applicationId "co.netguru.baby.monitor"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode isBitrise ? Integer.parseInt(bitrise.io.buildNumber) : 1
        versionName '0.1.0'

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            versionNameSuffix "-DEBUG"
            applicationIdSuffix ".debug"
            testCoverageEnabled true
            debuggable true
            minifyEnabled true
            shrinkResources false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro', 'proguard-instant-run.pro', 'proguard-rules-test.pro'
            manifestPlaceholders = [HOCKEY_APP_ID: keyProperty("HOCKEY_APP_ID_DEBUG")]
        }
        releasePreprod {
            versionNameSuffix "-PREPROD"
            applicationIdSuffix ".preprod"
            debuggable true
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules-release-preprod.pro'
            manifestPlaceholders = [HOCKEY_APP_ID: keyProperty("HOCKEY_APP_ID_PREPROD")]
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules-release.pro'
            manifestPlaceholders = [HOCKEY_APP_ID: keyProperty("HOCKEY_APP_ID_PRODUCTION")]
        }
    }
    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    sourceSets.all {
        it.java.srcDir "src/$it.name/kotlin"
    }
    junitJacoco {
        jacocoVersion = '0.8.1'
        excludes = ['android/databinding/**/*.class',
                    '**/android/databinding/*Binding.class',
                    '**/BR.*',
                    '**/R.class',
                    '**/R$*.class',
                    '**/BuildConfig.*',
                    '**/Manifest*.*',
                    '**/*$ViewInjector*.*',
                    '**/*$ViewBinder*.*',
                    '**/*_MembersInjector.class',
                    '**/Dagger*Component.class',
                    '**/Dagger*Component$Builder.class',
                    '**/*Module_*Factory.class',
                    '**/AutoValue_*.*',
                    '**/*JavascriptBridge.class',
                    '**/Lambda$*.class',
                    '**/Lambda.class',
                    '**/*Lambda.class',
                    '**/*Lambda*.class']
    }
    lintOptions {
        abortOnError false
    }
}

kapt {
    correctErrorTypes = true
}

androidExtensions {
    experimental = true
}

configurations {
    dependencyUpdates.resolutionStrategy = {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }
}

dependencies {
    implementation project(':visualizer')

    //Kotlin
    implementation libs.kotlin
    implementation libs.constraintLayout

    //Live Data
    implementation libs.lifecycleExtensions
    kapt proc.lifecycleExtensions

    //Room
    implementation libs.room
    implementation libs.roomRx
    kapt proc.room

    // Support
    implementation libs.appCompat
    implementation libs.design

    // Dagger
    implementation libs.dagger
    implementation libs.daggerAndroid
    implementation libs.daggerAndroidSupport
    kapt proc.dagger
    kapt proc.daggerAndroid
    compileOnly proc.javaxAnnotation

    //RxJava
    implementation libs.rxJava2
    implementation(libs.rxKotlin2) {
        exclude module: 'rxjava'
        exclude module: 'kotlin-stdlib'
    }
    implementation(libs.rxAndroid2) {
        exclude module: 'rxjava'
    }

    // Timber
    implementation libs.timber

    // Stetho
    debugImplementation libs.stetho

    // LeakCanary
    debugImplementation libs.leakCanary

    // BlockCanary
    debugImplementation libs.blockCanary

    // StrictMode
    debugImplementation libs.strictmodeNotifier

    // HockeyApp
    implementation libs.hockeyApp

    //MaterialEditText
    implementation libs.materialEditText

    //Glide
    implementation libs.glide
    kapt proc.glide

    //ThreeTen
    implementation libs.threeTen

    //Expandable Layout
    implementation libs.expandableLayout

    //Gson
    implementation libs.gson

    //EasyImage
    implementation libs.easyImage

    //Navigation Components
    implementation libs.navigationFragment
    implementation libs.navigationUiKtx

    //Tensor Flow
    implementation libs.tensorFlow

    //Java Web Socket
    implementation libs.javaWebSocket

    //Material Dialogs
    implementation libs.materialDialogs

    //ExoPlayer
    implementation libs.exoPlayer

    //WebRtc
    implementation libs.googleWebRtc

    //BubblePagerIndicator
    implementation libs.bubblePagerIndicator

    //Firebase Storage
    implementation libs.firebaseStorage

    //Firebase Database
    implementation libs.firebaseDatabase

    //Unit testing
    testImplementation test.junit
    testImplementation test.mockito
    testImplementation test.mockitoKotlin

    //Instrumented tests
    androidTestImplementation test.supportAnnotations
    androidTestImplementation test.runner
    androidTestImplementation test.rules
    androidTestImplementation test.espresso
    androidTestImplementation test.room
    androidTestImplementation test.lifecycleExtensions
}
