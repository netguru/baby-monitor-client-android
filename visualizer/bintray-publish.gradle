apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId GROUP
            version VERSION_NAME
            artifactId ARTIFACT_ID
            artifact "build/outputs/aar/visualizer-release.aar"
            artifact androidJavadocsJar
            artifact androidSourcesJar
            pom.withXml {
                Node root = asNode()
                root.appendNode('name', ARTIFACT_ID)
                root.appendNode('description', POM_DESCRIPTION)
                root.appendNode('url', POM_URL)

                def issues = root.appendNode('issueManagement')
                issues.appendNode('system', 'github')
                issues.appendNode('url', ISSUE_URL)

                def scm = root.appendNode('scm')
                scm.appendNode('url', POM_SCM_URL)
                scm.appendNode('connection', POM_SCM_CONNECTION)
                scm.appendNode('developerConnection', POM_SCM_DEV_CONNECTION)

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', POM_LICENCE_NAME)
                license.appendNode('url', POM_LICENCE_URL)
                license.appendNode('distribution', POM_LICENCE_DIST)
            }
        }
    }
}

def getBintrayUserProperty() {
    return localProperty("bintray.user") ?: bintrayUser ?: "";
}

def getBintrayApiKeyProperty() {
    return localProperty("bintray.api.key") ?: bintrayApiKey ?: "";
}

def getGpgPassphrase() {
    return localProperty("bintray.gpg.pass") ?: gpgPassphrase ?: "";
}

bintray {
    user = getBintrayUserProperty()
    key = getBintrayApiKeyProperty()
    publications = ['mavenJava']

    dryRun = false
    publish = false
    pkg {
        repo = 'maven'
        name = ARTIFACT_ID
        licenses = ['Apache-2.0']
        labels = ['android', 'visualizer']

        version {
            name = VERSION_NAME
            vcsTag = VERSION_NAME
            gpg {
                sign = true
                passphrase = getGpgPassphrase()
            }
        }
    }
}
